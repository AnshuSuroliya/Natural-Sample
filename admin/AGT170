0010 * PROGRAM : AGT170
0020 * CREATED : 12/3/93
0030 * AUTHOR  : N.K.YABLONSKI
0040 * FUNCTION: ONLINE REVIEW OF AGENT AIR STATS FILE, USING 13 WEEK
0050 *           SELECTION OR CURRENT MONTH IE, CARRIER SALES.
0060 * MAPS    : AGT170M, AGT170M1, AGT170M2
0070 * TASK    : A1828T
0080 ************************************************************************
0090 * MEMO: A2571     YEAR 2000 CONVERSION     C*V*U*F    NANBOB  1997/1998
0100 ***********************************************************************
0110 /*---------------------------------------------------------------------*
0120 /* MEMO:          TAM00001                                             *
0130 /* PROJECT:       APS FIX - 13 PED LIMIT                               *
0140 /* PROGRAMMER:    AARON BURGESS                                        *
0150 /* DATE:          02/16/00                                             *
0160 /*                                                                     *
0170 /* DESCRIPTION:   1) UPDATE PROGRAM TO CORRECTLY LIMIT PED SELECTION   *
0180 /*                   LIMIT TO 13 PEDS.                                 *
0190 /*---------------------------------------------------------------------*
0200 /* MEMO:          TAM00001                                             *
0210 /* PROJECT:       APS FIX - 13 PED LIMIT                               *
0220 /* PROGRAMMER:    AARON BURGESS                                        *
0230 /* DATE:          03/13/00                                             *
0240 /*                                                                     *
0250 /* DESCRIPTION:   1) UPDATE PROGRAM TO USE TEMPORARY TABLE FOR PED     *
0260 /*                   SELECTION TO LIMIT THE NUMBER OF PEDS TO 13.      *
0270 /*---------------------------------------------------------------------*
0280 * ISD99040B INCREASE AREA BANK TO 1-25 & 2 BYTES#T R. J. HAFEY#M 11/1999
0290 * RESTOW FOR 2 CHR AREA-BANK AND MAP CHANGE
0300 ************************************************************************
0310 /* PROJECT:       EASE #234937                                         *
0320 /* PROGRAMMER:    THOMAS STOMBAUGH                                     *
0330 /* DATE:          08/17/09                                             *
0340 /*                                                                     *
0350 /* DESCRIPTION:   1) INCREASE OCCURS FOR #STAT-WEEKT FROM 50 TO 99     *
0360 /*---------------------------------------------------------------------*
0370 DEFINE DATA
0380 GLOBAL USING ARC000G
0390 LOCAL
0400 1 H-AAS-STA VIEW OF  AGENT-AIRLINE-STATS
0410   2 AAS-EFFECTIVE-DATE
0420 1 AAS-STA VIEW OF  AGENT-AIRLINE-STATS
0430   2 AAS-AGENT-STATE
0440   2 AAS-AGENT-CODE-5
0450   2 AAS-REPORT-DATE
0460   2 REDEFINE AAS-REPORT-DATE
0470     3 #FIL (A4)                                                 /* A2571
0480     3 AAS-REPORT-MM (A2)
0490   2 AAS-AREA-BANK
0500   2 AAS-AIRLINE-CODE
0510   2 AAS-EFFECTIVE-DATE
0520   2 AAS-DOM-CASH-FARES
0530   2 AAS-DOM-CRED-FARES
0540   2 AAS-INTL-CASH-FARES
0550   2 AAS-INTL-CRED-FARES
0560   2 AAS-NET-REMITTANCE
0570   2 AAS-COMMISSION
0580   2 AAS-NON-CARRIER-COMM
0590 1 AGT-STAT VIEW OF AGENT-STATISTICS
0600   2 AGS-AGENT-STATE
0610   2 AGS-AGENT-CODE-5
0620   2 AGS-PROCESSING-MONTH
0630   2 AGS-PROCESSING-WEEK
0640   2 REDEFINE AGS-PROCESSING-WEEK
0650     3 AGS-PROCESSING-WEEK-A (A1)
0660   2 AGS-PROCESSING-CYCLE
0670   2 REDEFINE AGS-PROCESSING-CYCLE
0680     3 AGS-PROCESSING-CYCLE-A (A1)
0690   2 AGS-EFFECTIVE-DATE
0700   2 AGS-REPORT-DATE
0710 1 AUTH VIEW OF AUTHORIZATION
0720   2 AUT-MONTHS-CURR-YR (1:12)
0730   2 AUT-MONTHS-PREV-YR (1:12)
0740 1 #AGT-ACN-NUM-ATTR(C)
0750 1 #AGT-PED-ATTR    (C)
0760 1 #CNTL            (C)
0770 1 #ACNAIR(A10)
0780 1 REDEFINE #ACNAIR
0790   2 #ACN-ST(A2)
0800   2 #ACN-CODE(A5)
0810   2 #AIR (A3)
0820 1 #S-DATE (A8)                                                  /* A2571
0830 1 #E-DATE (A8)                                                  /* A2571
0840 1 #MA   (A2)
0850 1 REDEFINE #MA
0860   2 #M  (N2)
0870 1 #S         (I1)
0880 1 #E         (I1)
0890 1 #I         (I1)
0900 1 #H         (I1)
0910 1 #J         (I1)
0920 1 #X         (I1)
0930 1 #IDX       (I1)   INIT<1>
0940 1 #IDX2      (I1)   INIT<13>
0950 1 #CNT       (I1)
0960 1 #POS       (I1)
0970 1 #NUM       (I1)
0980 1 #ALL       (A1)             /* SELECT ALL WEEKS
0990 1 #LOAD (L) INIT <TRUE>
1000 1 #TOTAL(L)
1010 1 #KEY-ARY   (A18/20)                                           /* A2571
1020 1 #ISN-ARY   (P10/20)
1030 1 #MTH       (A3/13)
1040 1 #H-MTH     (A2)
1050 1 #STAT-SEL-CNTL(C/20)
1060 1 #STAT-SEL  (A1/20)          /* SELECTION ON SCREEN
1070 1 #STAT-WEEK (D/20)          /* WEEKS ON AGT/AIR/STAT FILE SHOULD BE 13
1080 1 #STAT-WEEKT (D/99)
1090 1 #HLD-KEY(A16)
1100 1 #AAS-KEY(A18)              /* LAST WEEK IS MOST CURRENT       /* A2571
1110 1 #AGT-KEY(A15)                                                 /* A2571
1120 1 #RPT-PED    (A8/13)
1130 1 #REF-PID    (A8/13)
1140 1 #DOM-FARES  (N11.2/13)
1150 1 #INTL-FARES (N11.2/13)
1160 1 #TOT-COMM   (N11.2/13)
1170 1 #NET-REMT   (N9.2/13)
1180 1 #DOM-TOT (N13.2)
1190 1 #INTL-TOT(N13.2)
1200 1 #COMM-TOT(N13.2)
1210 1 #NET-TOT(N11.2)
1220 1 #BGN-WEEK         (A6)   /* VAR USED FOR MONTH SELECTION
1230 1 #END-WEEK         (A6)
1240 1 #DATD (D)
1250 1 #IND (A2)
1260 1 REDEFINE #IND
1270   2 #INX (N2)
1280 1 #TYPE             (A1) INIT<'M'>
1290 1 #DAYS             (N3)
1300 1 #DATE-IN          (A6)
1310 1 #START            (A6)
1320 1 REDEFINE #START
1330   2 #START-MM       (A2)
1340   2 #START-DD       (A2)
1350   2 #START-YY       (A2)
1360 1 #END              (A6)
1370 1 #RETRY            (A1)
1380 END-DEFINE
1390 INCLUDE PFKEY
1400 INCLUDE ARC999
1410 SET KEY PF5 PF7 PF8
1420 MOVE *PROGRAM TO #PROGRAM
1430 #STAT-SEL-CNTL(*) := (AD=NP)
1440 HISTOGRAM H-AAS-STA AAS-EFFECTIVE-DATE
1450   ADD 1 TO #H
1460   MOVE EDITED AAS-EFFECTIVE-DATE TO #STAT-WEEKT(#H)(EM=YYYYMMDD)
1470 END-HISTOGRAM
1480 ****** NO. OF WEEKS SHOULD BE 13, IF MORE, ONLY
1490 ****** SHOW 13 WEEKS,  CURRENT + 12 PRIOR WEEKS.
1500 IF #H > 13
1510   #S := (#H - 13) + 1
1520   #E := 13 + (#H - 13)
1530   #STAT-WEEK(#IDX:#IDX2) := #STAT-WEEKT(#S:#E)
1540   RESET #STAT-WEEK(14:20)
1550   #S := 1
1560   #E := 13
1570 ELSE
1580   #S := 1
1590   #E := *COUNTER(1440)
1600   #STAT-WEEK(#S:#E) := #STAT-WEEKT(#S:#E)
1610 END-IF
1620 *
1630 #STAT-SEL-CNTL(#S:#E) := (AD=I)
1640 REPEAT
1650   ASSIGN #PREV-SCREEN-NBR = #SCREEN-NBR
1660   ASSIGN #PREV-KEY        = #KEY
1670   ASSIGN #PREV-ACTION     = #ACTION
1680   ASSIGN #PREV-PROG       = *PROGRAM
1690 *
1700   INPUT TEXT #ERR-MSG MAP 'AGT170M'
1710   RESET #ERR-MSG
1720   IF *PF-KEY = 'PF5'
1730     FETCH RETURN 'AGT173'
1740     ESCAPE TOP
1750   END-IF
1760   IF *PF-KEY = 'PF3' OR = 'PF15'
1770     MOVE '99 '   TO #SCREEN-NBR
1780     MOVE *PROGRAM TO #PREV-PROG
1790     ESCAPE ROUTINE
1800   END-IF
1810   IF #SCREEN-NBR NOT = 'T17' OR #ACTION NOT = 'I'
1820     THEN MOVE *PROGRAM TO #PREV-PROG
1830     ESCAPE ROUTINE IMMEDIATE
1840   ELSE
1850     IGNORE
1860   END-IF
1870   IF #KEY = ' ' OR #KEY NE MASK(NNNNNNNNNN'  ')
1880     REINPUT 'PLEASE ENTER AGENT / AIRLINE WITHOUT CHECK DIGITS'
1890       MARK *#KEY
1900   END-IF
1910   IF NOT #STAT-SEL(*) > ' ' AND #ALL = ' ' AND #PED = ' '
1920     REINPUT 'PLEASE MAKE "DATE" SELECTION OR "ALL" SELECTION'
1930       MARK *#STAT-SEL(*)
1940   END-IF
1950   IF #STAT-SEL(*) > ' ' AND #ALL = ' '
1960     IF #PED NE ' '
1970       REINPUT 'CANNOT SELECT "DATES" AND "MONTH" TOGETHER'
1980         MARK *#PED
1990     END-IF
2000     EXAMINE #STAT-SEL(#S:20) FOR 'X' GIVING NUMBER #NUM INDEX #POS
2010     IF #NUM = 0
2020       REINPUT 'PLEASE ENTER AN "X"' MARK *#STAT-SEL(*)
2030     END-IF
2040     RESET #CNT
2050     FOR #X #POS 20
2060       ADD 1 TO #CNT
2070       IF #STAT-SEL(#X) = ' '
2080         IF #NUM NE #CNT - 1
2090           REINPUT 'SELECTION MUST BE CONTIGUOUS' MARK *#STAT-SEL(#X)
2100         ELSE
2110           ESCAPE BOTTOM
2120         END-IF
2130       END-IF
2140     END-FOR
2150    MOVE EDITED #STAT-WEEK(#POS)(EM=YYYYMMDD)TO #S-DATE          /*UA2571
2160    MOVE EDITED #STAT-WEEK(#X - 1)(EM=YYYYMMDD)TO #E-DATE        /*UA2571
2170   END-IF
2180   IF #ALL NE ' '
2190     IF #STAT-SEL(*) NE ' '
2200       REINPUT 'CANNOT SELECT "ALL" AND "DATES" TOGETHER'
2210         MARK *#STAT-SEL(*)
2220     END-IF
2230     IF #PED = MASK(MMDDYY)
2240       REINPUT 'CANNOT SELECT "ALL" AND "MONTH" TOGETHER'
2250         MARK *#PED
2260     END-IF
2270   MOVE EDITED #STAT-WEEK(#S)(EM=YYYYMMDD)TO #S-DATE             /*UA2571
2280   MOVE EDITED #STAT-WEEK(#E)(EM=YYYYMMDD)TO #E-DATE             /*UA2571
2290   END-IF
2300   IF #PED > ' '
2310     IF #PED = MASK(MM....)
2320       #MA := #PED
2330       READ AUTH
2340         IF #MA  = '12'
2350         MOVE EDITED AUT-MONTHS-PREV-YR(#M) TO #DATE(EM=YYYYMMDD)/*UA2571
2360           #M := 1
2370         ELSE
2380         MOVE EDITED AUT-MONTHS-CURR-YR(#M) TO #DATE(EM=YYYYMMDD)/*UA2571
2390           ADD 1 TO #M
2400         END-IF
2410       MOVE EDITED #DATE(EM=YYYYMMDD)TO #S-DATE                  /*UA2571
2420       MOVE EDITED AUT-MONTHS-CURR-YR(#M) TO #DATE(EM=YYYYMMDD)  /*UA2571
2430       MOVE EDITED #DATE(EM=YYYYMMDD)TO #E-DATE                  /*UA2571
2440       END-READ
2450     ELSE
2460       REINPUT 'PLEASE ENTER A VALID MONTH' MARK *#PED
2470     END-IF
2480   END-IF
2490   RESET #J #DOM-TOT #INTL-TOT #COMM-TOT #NET-TOT
2500     #RPT-PED(*) #DOM-FARES(*) #INTL-FARES(*) #TOT-COMM(*)
2510     #NET-REMT(*) #MTH(*)
2520   RESET INITIAL #LOAD
2530   #ACNAIR := #KEY
2540   COMPRESS #ACNAIR #S-DATE INTO #AAS-KEY LEAVE NO
2550   REPEAT
2560     READ  AAS-STA BY AAS-AGENT-AIR-EFF-KEY = #AAS-KEY
2570       IF (#ACN-ST NE AAS-AGENT-STATE) OR (#ACN-CODE NE AAS-AGENT-CODE-5)
2580           OR (AAS-AIRLINE-CODE NE #AIR )
2590         SUBTRACT 1 FROM *COUNTER(2560)
2600         ESCAPE BOTTOM
2610       END-IF
2620       IF AAS-EFFECTIVE-DATE > #E-DATE
2630         ESCAPE BOTTOM
2640       END-IF
2650       IF #PED = MASK(MM....)
2660         REJECT IF (AAS-EFFECTIVE-DATE = #E-DATE
2670           AND AAS-REPORT-DATE >= #E-DATE) OR
2680           (AAS-EFFECTIVE-DATE = #S-DATE
2690           AND AAS-REPORT-DATE < #S-DATE)
2700       END-IF
2710       REJECT IF *PF-KEY = 'PF8' AND #ISN-ARY(#J) NE *ISN AND #I = 0
2720       IF #I = 13
2730         COMPRESS AAS-AGENT-STATE AAS-AGENT-CODE-5 AAS-AIRLINE-CODE
2740           AAS-EFFECTIVE-DATE INTO #KEY-ARY(#J+1) LEAVE NO
2750         #ISN-ARY(#J+1) := *ISN
2760         IF *PF-KEY = 'PF8'
2770           SUBTRACT #DOM-FARES(*) FROM #DOM-TOT
2780           SUBTRACT #INTL-FARES(*) FROM #INTL-TOT
2790           SUBTRACT #TOT-COMM(*) FROM #COMM-TOT
2800           SUBTRACT #NET-REMT(*) FROM  #NET-TOT
2810         END-IF
2820         PERFORM MAP
2830         ADD #DOM-FARES(*) TO #DOM-TOT
2840         ADD #INTL-FARES(*) TO #INTL-TOT
2850         ADD #TOT-COMM(*) TO #COMM-TOT
2860         ADD #NET-REMT(*) TO #NET-TOT
2870         RESET #DOM-FARES(*) #INTL-FARES(*) #TOT-COMM(*) #NET-REMT(*)
2880         IF *PF-KEY = 'PF8'
2890           ESCAPE BOTTOM
2900         END-IF
2910       END-IF
2920       ADD 1 TO #I
2930       IF #LOAD AND #I = 1   /* ONLY LOAD FIRST REC OF SCR IF NOT THEIR
2940         ADD 1 TO #J
2950         COMPRESS AAS-AGENT-STATE AAS-AGENT-CODE-5 AAS-AIRLINE-CODE
2960           AAS-EFFECTIVE-DATE INTO #KEY-ARY(#J) LEAVE NO
2970         #ISN-ARY(#J) := *ISN
2980         RESET #LOAD
2990       END-IF
3000       COMPRESS AAS-AGENT-STATE AAS-AGENT-CODE-5 AAS-EFFECTIVE-DATE INTO
3010         #AGT-KEY LEAVING NO
3020       FIND AGT-STAT WITH AGS-AGENT-EFF-KEY = #AGT-KEY
3030         COMPRESS AGS-PROCESSING-MONTH '-' AGS-PROCESSING-WEEK-A
3040           '-' AGS-PROCESSING-CYCLE-A INTO #REF-PID(#I) LEAVE NO
3050       END-FIND
3060       IF AAS-REPORT-MM NE #H-MTH
3070         #H-MTH := AAS-REPORT-MM
3080       MOVE EDITED AAS-REPORT-DATE TO #DATD(EM=YYYYMMDD)         /*UA2571
3090         MOVE EDITED #DATD (EM=L(3)) TO #MTH(#I)
3100       END-IF
3110       MOVE SUBSTRING(AAS-REPORT-DATE,3,6) TO #RPT-PED(#I)       /*UA2571
3120       #DOM-FARES(#I) := AAS-DOM-CASH-FARES + AAS-DOM-CRED-FARES
3130       #INTL-FARES(#I):= AAS-INTL-CASH-FARES + AAS-INTL-CRED-FARES
3140       #TOT-COMM  (#I):= AAS-NON-CARRIER-COMM + AAS-COMMISSION
3150       #NET-REMT (#I) := AAS-NET-REMITTANCE
3160     END-READ
3170     IF NOT *PF-KEY = 'PF8'
3180       IF *COUNTER(2560) = 0
3190         #ERR-MSG := 'NO RECORDS FOUND'
3200         ESCAPE BOTTOM
3210       ELSE
3220         ADD #DOM-FARES(*) TO #DOM-TOT
3230         ADD #INTL-FARES(*) TO #INTL-TOT
3240         ADD #TOT-COMM(*) TO #COMM-TOT
3250         ADD #NET-REMT(*) TO #NET-TOT
3260         #TOTAL := TRUE
3270         PERFORM MAP
3280       END-IF
3290     END-IF
3300     DEFINE MAP
3310     IF #TOTAL
3320       INPUT MAP 'AGT170M2'
3330     ELSE
3340       IF #J = 1
3350         RESET #DOM-TOT #INTL-TOT #COMM-TOT #NET-TOT
3360       END-IF
3370       INPUT MAP 'AGT170M1'
3380     END-IF
3390     RESET #RPT-PED(*) #I #PED
3400       #REF-PID(*) #STAT-SEL(*) #ALL #MTH(*) #H-MTH
3410     DECIDE ON FIRST *PF-KEY
3420       VALUE 'ENTR'
3430         RESET #TOTAL #LOAD #KEY-ARY(*)
3440         ESCAPE BOTTOM(2550)
3450       VALUE 'PF7'
3460         IF #TOTAL
3470           REINPUT 'CANNOT SCROLL ANY FURTHER, PRESS PF8 OR ENTER'
3480         END-IF
3490         ADD 1 TO #J
3500       VALUE 'PF8'
3510         IF #J = 1
3520           REINPUT 'CANNOT SCROLL ANY FURTHER, PRESS PF7 OR ENTER'
3530         END-IF
3540         SUBTRACT #DOM-FARES(*) FROM #DOM-TOT
3550         SUBTRACT #INTL-FARES(*) FROM #INTL-TOT
3560         SUBTRACT #TOT-COMM(*) FROM #COMM-TOT
3570         SUBTRACT #NET-REMT(*) FROM  #NET-TOT
3580         RESET #TOTAL
3590         #J := #J - 1
3600         #AAS-KEY := #KEY-ARY(#J)       /* PREV SCR FROM LAST
3610       NONE IGNORE
3620     END-DECIDE
3630     END-SUBROUTINE
3640   END-REPEAT
3650 END-REPEAT
3660 END
